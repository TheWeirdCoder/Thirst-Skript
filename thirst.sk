options:
    do_dirty_bottle_debuff: true
    dirty_bottle_thirst_regen: 15
    clean_bottle_thirst_regen: 25
    thirst_effect: 5
    thirst_damage: 2

on load:
    set {-dirtwater} to water bottle named "&6Dirty Water"
    dye {-dirtwater} rgb(158,74,40)
    set {-water} to water bottle named "&bFresh Water"
    dye {-water} rgb(0,255,251)
    register furnace recipe for {-water} using {-dirtwater} with id "dirt_Water" with cook time 2 seconds
 
on right click with empty bottle:
    target block is water
    cancel event
    if distance between player's target block and player <= 4: 
        remove 1 of tool of player from player
        set {_temp} to water bottle named "&6Dirty Water"
        dye {_temp} rgb(158,74,40)
        give player 1 of {_temp}

command setthrist <integer> [<player>]:
    permission: op
    trigger:
        arg-2 is set:
            set {thirst::%arg-2's uuid%} to arg-1
        else:
            set {thirst::%player's uuid%} to arg-1
 
every 30 seconds:
    loop all players: 
        if {thirst::%uuid of loop-player%} > 0:            
            remove {@thirst_effect} from {thirst::%uuid of loop-player%}
            if {thirst::%uuid of loop-player%} < 0: 
                set {thirst::%uuid of loop-player%} to 0
 
every 2 seconds:
    loop all players: 
        if {thirst::%uuid of loop-player%} = 0:    
            damage loop-player by {@thirst_damage}
 
on consume:
    name of player's held item is "&6Dirty Water":
        player's held item is water bottle:
            if {thirst::%player's uuid%} < 100:
                add {@dirty_bottle_thirst_regen} to {thirst::%player's uuid%}
                if {thirst::%player's uuid%} > 100:
                    set {thirst::%player's uuid%} to 100
            chance of 27%:
                apply poison of tier 1 to player for 10 seconds if {@do_dirty_bottle_debuff} is true
            chance of 36%:
                apply nausea of tier 5 to player for 24 second if {@do_dirty_bottle_debuff} is true
    else if event-item is water bottle named "&bFresh Water":
        if {thirst::%player's uuid%} < 100:
            add {@clean_bottle_thirst_regen} to {thirst::%player's uuid%}
            if {thirst::%player's uuid%} > 100:
                set {thirst::%player's uuid%} to 100

on join:
    set {thirst::%player's uuid%} to 100 if {thirst::%player's uuid%} is not set
    while player is online:
        setProgressBar(player)
        send action bar "%{test::progressbar::%player's uuid%}%" to player
        wait 1 second
 
function setProgressBar(p: player):
    set {_percent} to 100 - {thirst::%{_p}'s uuid%}
    set {_progress} to "&8["
    set {_stepSize} to 10
    set {_steps} to {_percent} / {_stepSize}
    loop {_stepSize} times:
        if {_percent} >= {_stepSize} * loop-number:
            set {_progress} to "%{_progress}%&8ðŸ§ª"
        else if {_percent} >= {_stepSize} * loop-number - {_stepSize} / 2:
            set {_progress} to "%{_progress}%<##0599e6>ðŸ§ª"
        else:
            set {_progress} to "%{_progress}%&bðŸ§ª"
    set {_progress} to "%{_progress}%&8]"
    set {test::progressbar::%{_p}'s uuid%} to {_progress}